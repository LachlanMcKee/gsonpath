buildscript {
    ext.kotlin_version = '1.3.21'
    ext.javapoet_version = '1.11.1'
    ext.gson_version = '2.8.1'
    ext.junit_version = '4.12'

    ext.project_group = 'net.lachlanmckee'
    ext.core_version = '3.2.0'
    ext.compiler_base_version = '1.3.0'

    repositories {
        jcenter()
    }
}

plugins {
    id "org.sonarqube" version "2.6.2"
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.2"
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "Gsonpath"
        property "sonar.projectKey", "gsonpath"
    }
}

task integrationCodeCoverageReport(type: JacocoReport) {
    executionData fileTree(dir: "compiler/testing/integration").include("**/build/jacoco/*.exec")

    // Ignore extension code
    def excludes = ['gsonpath/compiler/ExtensionFieldMetadata.class']

    classDirectories = fileTree(dir: "compiler/standard/build/classes/java/main") +
            fileTree(dir: "compiler/standard/build/classes/kotlin/main") +
            fileTree(dir: "compiler/base/build/classes/java/main", excludes: excludes) +
            fileTree(dir: "compiler/base/build/classes/kotlin/main", excludes: excludes)

    sourceDirectories = files('compiler/standard/src/main/java',
            'compiler/base/src/main/java')

    reports {
        xml.setEnabled(true)
        xml.destination new File("${buildDir}/reports/jacoco/report.xml")
        html.setEnabled(false)
        csv.setEnabled(false)
    }

    dependsOn(':compiler:testing:integration:test')
}

task unitCodeCoverageReport(type: JacocoReport) {
    executionData fileTree(dir: "compiler/testing/unit").include("**/build/jacoco/*.exec")

    // Ignore extension code
    def excludes = ['gsonpath/compiler/ExtensionFieldMetadata.class']

    classDirectories = fileTree(dir: "compiler/standard/build/classes/java/main") +
            fileTree(dir: "compiler/standard/build/classes/kotlin/main") +
            fileTree(dir: "compiler/base/build/classes/java/main", excludes: excludes) +
            fileTree(dir: "compiler/base/build/classes/kotlin/main", excludes: excludes)

    sourceDirectories = files('compiler/standard/src/main/java',
            'compiler/base/src/main/java')

    reports {
        xml.setEnabled(true)
        xml.destination new File("${buildDir}/reports/jacoco/report.xml")
        html.setEnabled(false)
        csv.setEnabled(false)
    }

    dependsOn(':compiler:testing:unit:test')
}
