buildscript {
    ext.kotlin_version = '1.3.0'
    ext.javapoet_version = '1.11.1'
    ext.gson_version = '2.8.1'
    ext.junit_version = '4.12'

    ext.project_group = 'net.lachlanmckee'
    ext.core_version = '3.0.0'
    ext.compiler_base_version = '1.2.1'
    ext.extensions_version = '1.1.0'

    repositories {
        jcenter()
    }
}

plugins {
    id "org.sonarqube" version "2.6.2"
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.2"
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "Gsonpath"
        property "sonar.projectKey", "gsonpath"
    }
}

task coreIntegrationCodeCoverageReport(type: JacocoReport) {
    executionData fileTree(dir: "gsonpath-core/compiler/testing/integration-standard").include("**/build/jacoco/*.exec"),
            fileTree(dir: "gsonpath-core/compiler/testing/integration-extension").include("**/build/jacoco/*.exec")

    // Ignore extension code
    def excludes = ['gsonpath/compiler/ExtensionFieldMetadata.class']

    classDirectories = fileTree(dir: "gsonpath-core/compiler/standard/build/classes/java/main") +
            fileTree(dir: "gsonpath-core/compiler/standard/build/classes/kotlin/main") +
            fileTree(dir: "gsonpath-core/compiler/base/build/classes/java/main", excludes: excludes) +
            fileTree(dir: "gsonpath-core/compiler/base/build/classes/kotlin/main", excludes: excludes)

    sourceDirectories = files('gsonpath-core/compiler/standard/src/main/java',
            'gsonpath-core/compiler/base/src/main/java')

    reports {
        xml.setEnabled(true)
        xml.destination new File("${buildDir}/reports/jacoco/report.xml")
        html.setEnabled(false)
        csv.setEnabled(false)
    }

    dependsOn(':gsonpath-core:compiler:testing:integration-standard:test')
    dependsOn(':gsonpath-core:compiler:testing:integration-extension:test')
}

task coreUnitCodeCoverageReport(type: JacocoReport) {
    executionData fileTree(dir: "gsonpath-core/compiler/testing/unit-standard").include("**/build/jacoco/*.exec")

    // Ignore extension code
    def excludes = ['gsonpath/compiler/ExtensionFieldMetadata.class']

    classDirectories = fileTree(dir: "gsonpath-core/compiler/standard/build/classes/java/main") +
            fileTree(dir: "gsonpath-core/compiler/standard/build/classes/kotlin/main") +
            fileTree(dir: "gsonpath-core/compiler/base/build/classes/java/main", excludes: excludes) +
            fileTree(dir: "gsonpath-core/compiler/base/build/classes/kotlin/main", excludes: excludes)

    sourceDirectories = files('gsonpath-core/compiler/standard/src/main/java',
            'gsonpath-core/compiler/base/src/main/java')

    reports {
        xml.setEnabled(true)
        xml.destination new File("${buildDir}/reports/jacoco/report.xml")
        html.setEnabled(false)
        csv.setEnabled(false)
    }

    dependsOn(':gsonpath-core:compiler:testing:unit-standard:test')
}

task extensionsIntegrationCodeCoverageReport(type: JacocoReport) {
    executionData fileTree(dir: "gsonpath-extensions/sample").include("**/build/jacoco/*.exec")

    classDirectories = fileTree(dir: "gsonpath-extensions/compiler/build/classes/java/main") +
            fileTree(dir: "gsonpath-extensions/compiler/build/classes/kotlin/main")

    sourceDirectories = files('gsonpath-extensions/compiler/src/main/java')

    reports {
        xml.setEnabled(true)
        xml.destination new File("${buildDir}/reports/jacoco/report.xml")
        html.setEnabled(false)
        csv.setEnabled(false)
    }

    dependsOn(':gsonpath-extensions:sample:test')
}

task extensionsUnitCodeCoverageReport(type: JacocoReport) {
    executionData fileTree(dir: "gsonpath-extensions/compiler").include("**/build/jacoco/*.exec")

    classDirectories = fileTree(dir: "gsonpath-extensions/compiler/build/classes/java/main") +
            fileTree(dir: "gsonpath-extensions/compiler/build/classes/kotlin/main")

    sourceDirectories = files('gsonpath-extensions/compiler/src/main/java')

    reports {
        xml.setEnabled(true)
        xml.destination new File("${buildDir}/reports/jacoco/report.xml")
        html.setEnabled(false)
        csv.setEnabled(false)
    }
}