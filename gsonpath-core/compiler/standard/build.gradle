apply plugin: 'java'
apply plugin: "kotlin"
apply plugin: 'maven'
apply plugin: 'signing'

group = project_group
version = core_version

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile project(":gsonpath-core:gsonpath-core-library")
    compile project(":gsonpath-core:compiler:gsonpath-core-compiler-base")

    compile "com.squareup:javapoet:$javapoet_version"
    compile "com.google.auto.service:auto-service:1.0-rc2"
    compile "com.google.auto:auto-common:0.6"
    compile "com.google.code.gson:gson:$gson_version"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.artifactId = 'gsonpath-compiler'
                pom.project {
                    name 'gsonpath-compiler'
                    packaging 'jar'
                    description 'An annotation processor which generates Type Adapters for the Google Gson library'
                    url 'http://lachlanmckee.net/gsonpath/'

                    scm {
                        url 'scm:git@github.com:LachlanMcKee/gsonpath.git'
                        connection 'scm:git@github.com:LachlanMcKee/gsonpath.git'
                        developerConnection 'scm:git@github.com:LachlanMcKee/gsonpath.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'lachlanmckee'
                            name 'Lachlan McKee'
                        }
                    }
                }
            }
        }
    }
}